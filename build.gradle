import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

allprojects {
    group 'org.citygml4j.ade'
    version '1.0.2'
    description 'CityGML EnergyADE module for citygml4j'

    ext {
        citygml4jVersion = '2.11.3'
        date = new Date()
    }

    repositories {
        mavenCentral()
    }
}

sourceSets {
    main.java.srcDir 'src-gen/main/java'
}

dependencies {
    api "org.citygml4j:citygml4j:$citygml4jVersion"
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

javadoc {
    options {
        overview "$buildDir/tmp/javadoc/overview.html"
        header "$project.name $project.version"
        bottom """
            <a target="_blank" href="https://github.com/citygml4j/energy-ade-citygml4j/issues">Report a bug or suggest an enhancement</a><br />
            ${project.name} is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br />
            Official website and more information at: <a target="_blank" href="https://github.com/citygml4j">https://github.com/citygml4j</a><br />
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
        addStringOption('Xdoclint:none', '-quiet')
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$buildDir/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    name: project.name,
                    version: project.version
            ])
        }
    }
}

distributions.main.contents {
    from('resources/doc/README') {
        filter(ReplaceTokens, tokens: [
                name: project.name,
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: sourceCompatibility.toString(),
                citygml4jVersion: citygml4jVersion
        ])
    }
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
    into('specification') {
        from "$rootDir/resources/specification"
    }
    into('samples') {
        from tasks.getByPath(':energy-ade-samples:copySamples').outputs
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/citygml4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/citygml4j/energy-ade-citygml4j'
                    connection = 'scm:git:https://github.com/citygml4j/energy-ade-citygml4j.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype()
    }
}